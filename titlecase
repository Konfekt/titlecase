#!/usr/bin/perl
use 5.008001; use strict; use warnings; use utf8;

# Added localizations to https://github.com/ap/titlecase
our @SMALL_WORD_EN
	= qw/ (?<!q&)a an and as at(?!&t) but by en for if in of on or the to v[.]? via vs[.]? /;
our @SMALL_WORD_DE
   = qw/ und oder aber als bei mit nach von zu im in aus auf für mit bis über unter vor zum zur durch ohne gegen um an dem den des /;
our @SMALL_WORD_PT
   = qw/ a as ao aos com como da das de do dos e em na nas no nos o os por um uma uns umas /;
our @SMALL_WORD_ES
		= qw/ y e o u ni a as os las los de del al en con por para sobre entre sin hacia tras desde durante mediante según/;
our @SMALL_WORD_FR
		= qw/ à au aux avec chez dans de des du en et pour par sur vers sous /;
our @SMALL_WORD_IT
		= qw/ a al alla alle con da dal dall' delle dei degli del di in per su /;
our @SMALL_WORD = ( @SMALL_WORD_EN, @SMALL_WORD_PT, @SMALL_WORD_DE, @SMALL_WORD_ES, @SMALL_WORD_FR, @SMALL_WORD_IT );

my $apos = q/ (?: ['’] [[:lower:]]* )? /;

sub titlecase {
	my @str = @_ or return;

	for ( @str ) {
		s{\A\s+}{}, s{\s+\z}{};

		$_ = lc $_ unless /[[:lower:]]/;

		s{
			\b (_*) (?:
				( (?<=[ ][/\\]) [[:alpha:]]+ [-_[:alpha:]/\\]+ |   # file path or
				[-_[:alpha:]]+ [@.:] [-_[:alpha:]@.:/]+ $apos |    # URL, domain, or email or
				[0-9] [0-9,._ ]+ $apos )                           # a numeric literal
				|
				( (?i) ${\join '|', @SMALL_WORD} $apos )           # or small word (case-insensitive)
				|
				( [[:alpha:]] [[:lower:]'’()\[\]{}]* $apos )       # or word w/o internal caps
				|
				( [[:alpha:]] [[:alpha:]'’()\[\]{}]* $apos )       # or some other word
			) (?= _* \b )
		}{
			$1 .
			( defined $2 ? $2         # preserve URL, domain, or email
			: defined $3 ? lc $3      # lowercase small word
			: defined $4 ? ucfirst $4 # capitalize lower-case word
			: $5 )                    # preserve other kinds of word
		}exgo;

		# exceptions for small words: capitalize at start and end of title
		s{
			( \A [[:punct:]]*          # start of title...
			|  [:.;?!][ ]+             # or of subsentence...
			|  [ ]['"“‘(\[][ ]*     )  # or of inserted subphrase...
			( ${\join '|', @SMALL_WORD} ) \b  # ... followed by small word
		}{$1\u\L$2}xigo;

		s{
			\b ( ${\join '|', @SMALL_WORD} )  # small word...
			(?= [[:punct:]]* \Z   # ... at the end of the title...
			|   ['"’”)\]] [ ] )   # ... or of an inserted subphrase?
		}{\u\L$1}xigo;
	}

	wantarray ? @str : ( @str > 1 ) ? \@str : $str[0];
}

use open qw( :encoding(UTF-8) :std );
use Getopt::Std 'getopts';

getopts 'f', \my %opt;

while ( readline ) {
	$_ = lc if $opt{'f'};
	print titlecase( $_ ), "\n";
}
