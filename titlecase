#!/usr/bin/perl
use strict;
use warnings;
use utf8;
use open qw( :utf8 :std );

my @small_words = qw( (?<!q&)a an and as at(?!&t) but by en for if in of on or the to v[.]? via vs[.]? );
my $small_re = join '|', @small_words;

my $apos = qr/ (?: ['’] [[:lower:]]* )? /x;

while ( <> ) {
	s{
		\b (?:
			( [[:alpha:]]+ [@.:/] [[:alpha:]@.:/]+ $apos ) # URL, domain, or email
			|
			( (?i: $small_re ) $apos )                     # or small word (case-insensitive)
			|
			( [[:alpha:]] [[:lower:]'’]* $apos )           # or word w/o internal caps
			|
			( [[:alpha:]] [[:alpha:]'’]* $apos )           # or some other word
		) \b
	}{
		  defined $1 ? $1         # preserve URL, domain, or email
		: defined $2 ? "\L$2"     # lowercase small word
		: defined $3 ? "\u\L$3"   # capitalize word w/o internal caps
		: $4                      # preserve other kinds of word
	}exgo;


	# exceptions for small words: capitalize at start and end of title
	s{
		(  \A [[:punct:]]*        # start of title...
		|  [:.;?!][ ]+['"“’]?     # or of subphrase...
		|  [(][ ]*             )  # or of parenthesised subphrase...
		( $small_re ) \b          # ... followed by small word
	}{$1\u\L$2}xigo;

	s{
		\b ( $small_re )          # small word...
		(?= [[:punct:]]* \Z )     # ... at the end of the title?
	}{\u\L$1}xigo;


	print;
}
